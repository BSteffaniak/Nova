import "setjmp.h";
import "ArrayList";

public class ExceptionData
{
	external jmp_buf*	buffer;
	
	ExceptionData	parent;
	
	ArrayList		codes;
	
	public ExceptionData(external jmp_buf* buf)
	{
		buffer = buf;
		codes  = new ArrayList();
	}
	
	public ArrayList getCodes()
	{
		return codes;
	}
	
	public void addCode(int code)
	{
		codes.add(code);
	}
	
	public setjmp.jmp_buf getBuffer()
	{
		return buffer;
	}
	
	public ExceptionData getCorrectData(int code)
	{
		ExceptionData data = this;
		
		while (data != 0)
		{
			ArrayList list = data.getCodes();
			
			for (int i = 0; i < list.getSize(); i++)
			{
				if (list.get(i) == code)
				{
					return data;
				}
			}
			
			if (data.getParent() == 0)
			{
				return data;
			}
			
			data = data.getParent();
		}
		
		return 0;
	}
	
	public setjmp.jmp_buf getCorrectBuffer(int code)
	{
		ExceptionData data = getCorrectData(code);
		
		if (data == 0)
		{
			return 0;
		}
		
		return data.getBuffer();
	}
	
	public void jumpToBuffer(int code)
	{
		ExceptionData data = getCorrectData(code);
		
		if (data.getParent() == 0)
		{
			code = 1;
		}
		
		setjmp.jmp_buf* buf = data.getBuffer();
		
		setjmp.longjmp(*buf, code);
	}
	
	public ExceptionData getParent()
	{
		return parent;
	}
	
	public void setParent(ExceptionData p)
	{
		parent = p;
	}
}