package "example"

import "nova/standard/svg/SVG";
import "nova/standard/svg/SVGCircle";
import "nova/standard/svg/SVGComponentList"
import "nova/standard/io/File"
import "nova/standard/time/Timer"

public class SVGTest
{
	public static main(String args[])
	{
		SVG s = new SVG()
		
		Double pi2 = Math.PI * 2
		
		Console.write("Enter the number of vertices: ")
		Int    numVerts = Console.readInt()
		
		Console.write("Enter the coefficient: ")
		Double coefficient = Console.readDouble()
		
		Console.write("Enter the number of poInts: ")
		Int    numPoInts = Console.readInt()
		
		Timer timer = new Timer().start()
		
		Double poInts[] = new Double[numVerts * 2]
		Double radius   = 450
		Double offset   = pi2 / 12
		
		for (Int i = 0; i < numVerts; i++)
		{
			Double rad = pi2 * (i / (numVerts * 1.0)) + offset
			
			poInts[i * 2 + 0] = radius * Math.cos(rad) + radius + 10
			poInts[i * 2 + 1] = radius * Math.sin(rad) + radius + 10
		}
		
		for (Int i = 0; i < numVerts; i++)
		{
			Double x = poInts[i * 2 + 0]
			Double y = poInts[i * 2 + 1]
			
			SVGCircle circle2 = new SVGCircle(x, y, 3)
			
			s.root.children.addChild(circle2)
		}
		
		Double cx = radius + 10
		Double cy = radius + 10
		
		for (Int i = 0; i < numPoInts; i++)
		{
			Int rand = Math.random(numVerts)
			
			Double x = poInts[rand * 2 + 0]
			Double y = poInts[rand * 2 + 1]
			
			cx = cx - (cx - x) * coefficient
			cy = cy - (cy - y) * coefficient
			
			if (i > 15)
			{
				if (i % 1000 == 0)
				{
					Console.writeLine(i)
				}
				
				SVGCircle circle = new SVGCircle(cx, cy, 1)
				
				s.root.children.addChild(circle)
			}
		}
		
		File f = new File("C:/Users/Braden/svgoutput.html")

		f.create()
		f.clearContents()
		
		s.generateHTMLOutput(f)
		
		timer.stop()
		
		Console.write("Done generating " + timer.duration)
		
		Console.waitForEnter()
	}
}