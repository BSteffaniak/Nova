import "Thread.h";
import "IO";

public class Thread
{
	external FATHOM_THREAD_HANDLE*	handle;
	
	long	millis;
	
	String	word;
	
	external
	{
		typedef struct DataStruct DataStruct;
		
		struct DataStruct
		{
			Thread* instance;
		};
	}
	
	public Thread(long millis, String word)
	{
		this.millis = millis;
		this.word   = word;
	}
	
	public void start()
	{
		external FATHOM_THREAD_HANDLE* handle;
		
		external
		{
			DataStruct* data = (DataStruct*)malloc(sizeof(DataStruct));
			
			data->instance   = this;
			
			handle = (FATHOM_THREAD_HANDLE*)malloc(sizeof(FATHOM_THREAD_HANDLE));
			
			lib_fathom_thread_create(handle, lib_fathom_thread_run, (FATHOM_THREAD_FUNC_ARG)data);
		}
		
		this.handle = handle;
	}
	
	public void join()
	{
		external FATHOM_THREAD_HANDLE* handle = this.handle;
		
		external
		{
			lib_fathom_thread_join(*handle);
		}
	}
	
	public static void sleep(external long millis)
	{
		external
		{
			lib_fathom_thread_sleep(millis);
		}
	}
	
	public void run()
	{
		for (int i = 0; i < 10; i++)
		{
			IO.println(word);
			
			sleep(millis);
		}
	}
	
	external FATHOM_THREAD_FUNC lib_fathom_thread_run(external FATHOM_THREAD_FUNC_ARG arg)
	{
		external
		{
			DataStruct* data = (DataStruct*)arg;
			
			Thread* this = data->instance;
			ExceptionData* exceptionData = 0;
		}
		
		try
		{
			Thread.nova_Thread_run(this, exceptionData);
		}
		catch (Exception e)
		{
			IO.println("An error has occurred...");
		}
		
		external
		{
			free(data);
		}
		
		return 0;
	}
}