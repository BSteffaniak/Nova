package "nova/standard"

public class String
{
	visible Int    length
	
	visible Char[] chars
	
	static Int     whitespaceLength = 4
	static Char[]  whitespace       = [ ' ', '\t', '\n', '\r' ]
	
	external void strcpy(char dst[], char src[])
	external void strcat(char dst[], char src[])
	external int strlen(char chars[])
	external void memcpy(char dst[], char* src[], int length)
	
	public construct(Char c)
	{
		Char chars[] = new Char[2]
		
		chars[0] = c
		chars[1] = '\0'
		
		this(chars)
	}
	
	public construct(Char[] chars)
	{
		this.chars = chars
		
		length = calculateLength()
		
//		if (chars[length] != '\0')
//		{
//			char newData[] = new char[length + 1]
//			
//			strcpy(newData, chars)
//			
//			newData[length] = '\0'
//			
//			this.chars = newData
//		}
		
		if (chars[length] != '\0')
		{
			chars[length] = '\0'
		}
	}
	
	calculateLength() -> Int
	{
		return strlen(chars)
	}
	
	public concat(String str) -> String
	{
		str = str.toString()
		
		Int size = str.length + length + 1
		
		Char newData[] = new Char[size]
		
		strcpy(newData, chars)
		strcat(newData, str.chars)
		
		newData[size - 1] = '\0'
		
		String newStr = new String(newData)
		
		return newStr
	}
	
	public equals(String other) -> Bool
	{
		if (length != other.length)
		{
			return false
		}
		
		for (Int i = 0; i < length; i++)
		{
			if (chars[i] != other.chars[i])
			{
				return false
			}
		}
		
		return true
	}
	
	indexOf(String search, Int start, Int direction) -> Int
	{
		for (Int i = start; i < length && i >= 0; i = i + direction)
		{
			Bool found = true
					
			for (Int j = 0; j < search.length && i + j < length && found; j++)
			{
				if (search.chars[j] != chars[i + j])
				{
					found = false
				}
			}
			
			if (found)
			{
//				if (direction > 0)
//				{
					return i
//				}
//				else
//				{
//					return i - search.length + 1
//				}
			}
		}
		
		return -1
	}
	
	public indexOf(String search) -> Int
	{
		return indexOf(search, 0, 1)
	}
	
	public indexOf(String search, Int start) -> Int
	{
		return indexOf(search, start, 1)
	}
	
	public lastIndexOf(String search) -> Int
	{
		return indexOf(search, length - 1, -1)
	}
	
	public substring(Int start, Int end) -> String
	{
		if (end - start == 0)
		{
			return ""
		}
		else if (end - start < 0)
		{
			throw new Exception("Substring bounds of [" + start + ", " + end + "] are invalid")
		}
		
		Char buf[] = new Char[end - start + 1]
		
		memcpy(buf, chars[start], end - start)
		
		buf[end - start] = '\0'
		
		return new String(buf)
	}
	
	public substring(Int start) -> String
	{
		return substring(start, length)
	}
	
	public lastChar() -> Char
	{
		return charAt(length - 1)
	}
	
	public charAt(Int index) -> Char
	{
		return chars[index]
	}
	
	public trim() -> String
	{
		Int start = 0
		Int end   = length - 1
		
		while (start < length && containsChar(chars[start], whitespace, whitespaceLength))
		{
			start++
		}
		
		while (end >= 0 && containsChar(chars[end], whitespace, whitespaceLength))
		{
			end--
		}
		
		if (end == 0)
		{
			return ""
		}
		
		if (start == 0 && end == length - 1)
		{
			return this
		}
		
		return substring(start, end + 1)
	}
	
	static containsChar(Char needle, Char Chars[], Int length) -> Bool
	{
		for (Int i = 0; i < length; i++)
		{
			if (needle == Chars[i])
			{
				return true
			}
		}
		
		return false
	}
	
	public toLowerCase() -> String
	{
		return changeCase(Char.toLowerCase)
	}
	
	public toUpperCase() -> String
	{
		return changeCase(Char.toUpperCase)
	}
	
	changeCase(transform(Char) -> Char) -> String
	{
		Char newData[] = new Char[length]
		
		for (Int i = 0; i < length; i++)
		{
			newData[i] = transform(chars[i])
		}
		
		return new String(newData)
	}
	
	public getDataBetween(String before, String after) -> String
	{
		return getDataBetween(before, after, 0)
	}
	
	public getDataBetween(String before, String after, Int start) -> String
	{
		Int s = indexOf(before, start)
		Int e = indexOf(after, s + 1)
		
		if (s >= 0 && e > 0)
		{
			return substring(s + before.length, e)
		}
		
		return ""
	}
	
	public toString() -> String
	{
		return this
	}
}