package "nova/standard/datastruct"

public class BinaryNode<E> extends Node<E>
{
	/**
	 * The left child of the BinaryNode instance.
	 */
	public BinaryNode<E> left
	{
		get { return getNode(0) }
		set { setNode(0, value) }
	}
	
	/**
	 * The right child of the BinaryNode instance.
	 */
	public BinaryNode<E> right
	{
		get { return getNode(1) }
		set { setNode(1, value) }
	}
	
	public construct()
	{
		this(null)
	}
	
	public construct(E data)
	{
		this(data, 2)
	}
	
	construct(E data, Int size)
	{
		super(data, size)
	}
	
	getNode(Int index) -> BinaryNode<E>
	{
		if (children.size <= index)
			return null
		
		return (BinaryNode)children.get(index)
	}
	
	setNode(Int index, BinaryNode<E> data)
	{
		if (children.size <= index)
			children.add(index, data)
		else
			children.set(index, data)
	}
	
	public addChild(E data)
	{
		Comparable<E> temp = (Comparable)this.data
		
		if (temp.compareTo(data) >= 0)
		{
			if (left == null)
				left = new BinaryNode(data)
			else
				left.addChild(data)
		}
		else
		{
			if (right == null)
				right = new BinaryNode(data)
			else
				right.addChild(data)
		}
	}
	
	public inorder() -> String
	{
		String str = ""
		
		if (left != null)
			str = str + left.inorder() + ", "
		
		str = str + data
		
		if (right != null)
			str = str + ", " + right.inorder()
		
		return str
	}
	
	public postorder() -> String
	{
		String str = ""
		
		if (left != null)
			str = str + left.postorder() + ", "
		if (right != null)
			str = str + right.postorder() + ", "
		
		str = str + data
		
		return str
	}
}