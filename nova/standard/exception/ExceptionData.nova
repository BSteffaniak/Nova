package "nova/standard/exception"

import "nova/standard/datastruct/ArrayList"

public class ExceptionData
{
	external type buffer
	
	external void jump(buffer buf, int code)
	
	buffer					buf
	
	ExceptionData			parent
	
	public Exception		thrownException
	
	visible ArrayList<Int>	codes
	
	public construct(buffer buf)
	{
		this.buf = buf
		codes    = new ArrayList()
	}
	
	public addCode(int code)
	{
		codes.add(new Int(code))
	}
	
	public getBuffer() -> buffer
	{
		return buf
	}
	
	public getCorrectData(int code) -> ExceptionData
	{
		ExceptionData data = this
		
		while (data != null)
		{
			ArrayList<Int> list = data.codes
			
			for (int i = 0; i < list.size; i++)
			{
				if (list.get(i).value == code)
				{
					return data
				}
			}
			
			if (data.getParent() == null)
			{
				return data
			}
			
			data = data.getParent()
		}
		
		return 0
	}
	
	public getCorrectBuffer(int code) -> buffer
	{
		ExceptionData data = getCorrectData(code)
		
		if (data == null)
		{
			return 0
		}
		
		return data.getBuffer()
	}
	
	public jumpToBuffer(int code)
	{
		ExceptionData data = getCorrectData(code)
		
		if (data.getParent() == null)
		{
			code = 1
		}
		
		buffer b = data.getBuffer()
		
		jump(b, code)
	}
	
	public getParent() -> ExceptionData
	{
		return parent
	}
	
	public setParent(ExceptionData p)
	{
		parent = p
	}
}